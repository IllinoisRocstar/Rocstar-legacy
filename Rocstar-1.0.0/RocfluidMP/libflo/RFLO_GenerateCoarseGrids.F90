! *********************************************************************
! * Rocstar Simulation Suite                                          *
! * Copyright@2015, Illinois Rocstar LLC. All rights reserved.        *
! *                                                                   *
! * Illinois Rocstar LLC                                              *
! * Champaign, IL                                                     *
! * www.illinoisrocstar.com                                           *
! * sales@illinoisrocstar.com                                         *
! *                                                                   *
! * License: See LICENSE file in top level of distribution package or *
! * http://opensource.org/licenses/NCSA                               *
! *********************************************************************
! *********************************************************************
! * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   *
! * EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   *
! * OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          *
! * NONINFRINGEMENT.  IN NO EVENT SHALL THE CONTRIBUTORS OR           *
! * COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER       *
! * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,   *
! * Arising FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE    *
! * USE OR OTHER DEALINGS WITH THE SOFTWARE.                          *
! *********************************************************************
!******************************************************************************
!
! Purpose: generate coarse grids from the finest grid.
!
! Description: the coarse grids are generated by omitting every second
!              grid node in all three directions (standard coarsening).
!
! Input: region = current region.
!
! Output: region%levels%grid%xyz = grid coordinates.
!
! Notes: none.
!
!******************************************************************************
!
! $Id: RFLO_GenerateCoarseGrids.F90,v 1.3 2008/12/06 08:44:06 mtcampbe Exp $
!
! Copyright: (c) 2001 by the University of Illinois
!
!******************************************************************************

SUBROUTINE RFLO_GenerateCoarseGrids( region )

  USE ModDataTypes
  USE ModDataStruct, ONLY : t_region
  USE ModInterfaces, ONLY : RFLO_GetDimensPhysNodes, RFLO_GetNodeOffset
  USE ModError
  USE ModParameters
  IMPLICIT NONE

#include "Indexing.h"

! ... parameters
  TYPE(t_region) :: region

! ... loop variables
  INTEGER :: iLev, i, j, k

! ... local variables
  INTEGER :: ipnbegF, ipnendF, jpnbegF, jpnendF, kpnbegF, kpnendF
  INTEGER :: ipnbegC, ipnendC, jpnbegC, jpnendC, kpnbegC, kpnendC
  INTEGER :: iOffFine, ijOffFine, iOffCoarse, ijOffCoarse, ijkF, ijkC
  INTEGER :: ii, jj, kk

  REAL(RFREAL), POINTER :: xyzFine(:,:), xyzCoarse(:,:)

!******************************************************************************

  CALL RegisterFunction( region%global,'RFLO_GenerateCoarseGrids',&
  'RFLO_GenerateCoarseGrids.F90' )

! loop over higher levels (>1)

  DO iLev=2,region%nGridLevels

    xyzFine   => region%levels(iLev-1)%grid%xyz
    xyzCoarse => region%levels(iLev  )%grid%xyz

    CALL RFLO_GetDimensPhysNodes( region,iLev-1,ipnbegF,ipnendF, &
                                  jpnbegF,jpnendF,kpnbegF,kpnendF )
    CALL RFLO_GetDimensPhysNodes( region,iLev  ,ipnbegC,ipnendC, &
                                  jpnbegC,jpnendC,kpnbegC,kpnendC )
    CALL RFLO_GetNodeOffset( region,iLev-1,iOffFine  ,ijOffFine   )
    CALL RFLO_GetNodeOffset( region,iLev  ,iOffCoarse,ijOffCoarse )

    DO k=kpnbegC,kpnendC
      kk = (k-kpnbegC)*2 + kpnbegF
      DO j=jpnbegC,jpnendC
        jj = (j-jpnbegC)*2 + jpnbegF
        DO i=ipnbegC,ipnendC
          ii   = (i-ipnbegC)*2 + ipnbegF
          ijkF = IndIJK(ii,jj,kk,iOffFine  ,ijOffFine  )
          ijkC = IndIJK(i ,j ,k ,iOffCoarse,ijOffCoarse)
          xyzCoarse(XCOORD,ijkC) = xyzFine(XCOORD,ijkF)
          xyzCoarse(YCOORD,ijkC) = xyzFine(YCOORD,ijkF)
          xyzCoarse(ZCOORD,ijkC) = xyzFine(ZCOORD,ijkF)
        ENDDO  ! i
      ENDDO    ! j
    ENDDO      ! k

  ENDDO        ! iLev

! finalize

  CALL DeregisterFunction( region%global )

END SUBROUTINE RFLO_GenerateCoarseGrids

!******************************************************************************
!
! RCS Revision history:
!
! $Log: RFLO_GenerateCoarseGrids.F90,v $
! Revision 1.3  2008/12/06 08:44:06  mtcampbe
! Updated license.
!
! Revision 1.2  2008/11/19 22:17:20  mtcampbe
! Added Illinois Open Source License/Copyright
!
! Revision 1.1  2004/11/29 21:25:16  wasistho
! lower to upper case
!
! Revision 1.6  2003/11/20 16:40:34  mdbrandy
! Backing out RocfluidMP changes from 11-17-03
!
! Revision 1.3  2003/05/15 02:57:01  jblazek
! Inlined index function.
!
! Revision 1.2  2002/09/05 17:40:19  jblazek
! Variable global moved into regions().
!
! Revision 1.1  2002/06/07 16:40:36  jblazek
! Grid & solution for all regions in one file.
!
!******************************************************************************







