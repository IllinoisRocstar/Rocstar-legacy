#!/bin/csh
#
# Convert old Rocman control files for Rocman3
# Invoke this script from the Rocstar run directory.
#
# Usage: converter3 [parent directory of Rocman3/util]
#
# Written by Robert Fiedler, revised 9/15/08.
#.......................................................

if (! -d ./Rocman) then
  echo "Could not find Rocman directory."
  echo "Run this script in a Rocstar run directory."
  echo "Usage: converter3 [parent directory of Rocman3/util]"
  exit
endif

if ($#argv == 0 ) then
  set UTIL_PATH = $HOME/gen3/genx/Codes/Rocman3/util
  if (! -d $UTIL_PATH) then
    echo -n "Enter parent directory of Rocman3/util: "
    set UTIL_PATH = "$<"
    set UTIL_PATH = "${UTIL_PATH}/Rocman3/util"
  endif
else if ($#argv == 1) then
  set UTIL_PATH = $1
  set UTIL_PATH = "${UTIL_PATH}/Rocman3/util"
else
  echo "Usage: converter3 [parent directory of Rocman3/util]"
  exit
endif

if (! -e $UTIL_PATH/convert3) then
  echo "Could not find $UTIL_PATH/convert3"
  exit
endif
if (! -e $UTIL_PATH/convertman3) then
  echo "Could not find $UTIL_PATH/convertman3"
  exit
endif
echo "Using conversion utilities in $UTIL_PATH"

$UTIL_PATH/convert3 RocstarControl.txt
mv RocstarControl.txt RocstarControl.txt_2
mv RocstarControl.txt.new RocstarControl.txt_3
cp RocstarControl.txt_3 RocstarControl.txt

cd ./Rocman
$UTIL_PATH/convertman3 RocmanControl.txt
mv RocmanControl.txt RocmanControl.txt_2
mv RocmanControl.txt.new RocmanControl.txt_3
cp RocmanControl.txt_3 RocmanControl.txt

exit
